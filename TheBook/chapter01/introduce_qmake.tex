%使用xelatex编译
%版权所有，翻版必究
%本文件由程序自动生成，任何修改将被覆盖





%


\subsection{
qmake入门
}\label{s100310}


qmake类似于cmake，但qmake比cmake更加简洁清晰。
如果读者希望写一个跨平台的库的话，
或许cmake是比qmake更加优异的选择。
但读者明确是写一个特定的应用程序的话，
qmake就比cmake优秀的多。
qmake比cmake确实功能较少，
但从另一个角度，
qmake比cmake更加专注。
通过本节，
读者会发现只需要学习可怜的一点内容，
就可以使用qmake搭建出复杂的程序架构。



\subsubsection{
使用qmake构建Hellow World！
}\label{ss000610}

读者新建一个目录\footnote{
本书所有目录都要求不包含空格和中文，以后不再赘述。
}，
在此文件夹下新建一个“hellow\_world.pro”文件，输入文件内容如
\lstlistingname\ \ref{f000002}。
在此文件夹下建立“main.cpp”文件，输入内容如
\lstlistingname\ \ref{f000003}。

\begin{lstlisting}[label=f000002, 
caption=GoodLuck,
title=\lstlistingname\ \thelstlisting
]
QT -= gui
QT -= core

CONFIG += console

CONFIG(debug,debug|release){
    TARGET = hellow_word_debug
}else{
    TARGET = hellow_word
}

TEMPLATE = app

win32-msvc*{
    QMAKE_CXXFLAGS += /std:c++latest
}else{
    CONFIG += c++17
}

SOURCES += $$PWD/main.cpp
DESTDIR =  $$PWD

DEFINES *= NUMBER=1
DEFINES *= HELLOW=\\\"Hellow\\\"
DEFINES += QT_DEPRECATED_WARNINGS
\end{lstlisting}          %抄录环境

\begin{lstlisting}[label=f000003, 
caption=GoodLuck,
title=\lstlistingname\ \thelstlisting
]
#include <iostream>

int main(int , char **) {
    if constexpr(NUMBER) {
        std::cout << HELLOW " World! "
                  << std::endl;
    }
}
\end{lstlisting}          %抄录环境



\subsubsection{
使用qmake创建动态链接库
}\label{ss000710}



\subsubsection{
qmake高级用法
}\label{ss000810}


\begin{lstlisting}[label=f000004, 
caption=GoodLuck,
title=\lstlistingname\ \thelstlisting
]
TEMPLATE = subdirs

CONFIG += ordered

new_moc.file = $$PWD/new_moc/new_moc.pro
SUBDIRS += new_moc

before_run.file = $$PWD/before_run/before_run.pro
SUBDIRS += before_run

after_run.file = $$PWD/after_run/after_run.pro
SUBDIRS += after_run

the_run.file = $$PWD/the_run/the_run.pro
SUBDIRS += the_run
\end{lstlisting}          %抄录环境

\begin{lstlisting}[label=f000005, 
caption=GoodLuck,
title=\lstlistingname\ \thelstlisting
]
QT -= gui
QT -= core

CONFIG += console

CONFIG(debug,debug|release){
    TARGET = the_run_debug
}else{
    TARGET = the_run
}

TEMPLATE = app

win32-msvc*{
    QMAKE_CXXFLAGS += /std:c++latest
}else{
    CONFIG += c++17
    LIBS += -lstdc++fs
}

SOURCES += $$PWD/main.cpp
DESTDIR =  $$PWD/../bin

DEFINES += QT_DEPRECATED_WARNINGS

#when before build new_moc will call ...
new_moc.dependency_type = TYPE_C
new_moc.variable_out = SOURCES
new_moc.output  = moc_new_${QMAKE_FILE_BASE}.cpp
CONFIG(debug,debug|release){
new_moc.commands = \
$${DESTDIR}/new_moc_debug ${QMAKE_FILE_NAME} ${QMAKE_FILE_OUT}
}else{
new_moc.commands = \
$${DESTDIR}/new_moc ${QMAKE_FILE_NAME} ${QMAKE_FILE_OUT}
}
NEW_MOC_HEADERS = test2.hpp test1.hpp
new_moc.input = NEW_MOC_HEADERS
QMAKE_EXTRA_COMPILERS += new_moc

#when link started before_run will call ...
CONFIG(debug,debug|release){
    QMAKE_PRE_LINK += $${DESTDIR}/before_run_debug $$PWD
}else{
    QMAKE_PRE_LINK += $${DESTDIR}/before_run $$PWD
}
export(QMAKE_PRE_LINK)

#when link finished after_run will call ...
CONFIG(debug,debug|release){
    QMAKE_POST_LINK += $${DESTDIR}/after_run_debug $$PWD
}else{
    QMAKE_POST_LINK += $${DESTDIR}/after_run $$PWD
}
export(QMAKE_POST_LINK)
\end{lstlisting}          %抄录环境



\subsubsection{
qmake杂项
}\label{ss000910}










%使用xelatex编译
%版权所有，翻版必究
%本文件由程序自动生成，任何修改将被覆盖



